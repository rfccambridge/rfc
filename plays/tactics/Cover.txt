Metadata:
type tactic
name Cover

Parameters:

Objects:
defensebot (closest friendly (ourgoal) loose)
defensebotpt (pointof defensebot)

#goal geometry
GOALIE_X_POS (- (const-double GOALIE_X_POS_DELTA) (/ (const-double FIELD_WIDTH) 2))
goaltoppt (point GOALIE_X_POS 0.35)
goalbotpt (point GOALIE_X_POS -0.35)
vertgoalline (line goaltoppt goalbotpt)

#ball geometry
ballpoint (pointof ball)
ballgoalline (line ballpoint (ourgoal))
ballverticalpt (linelineintersection ballgoalline vertgoalline)
ballinterseptpt (pointdirectiondistance ballverticalpt ballpoint (* 0.3 (pointpointdistance ballverticalpt ballpoint)))

#ball-velocity projected geometry
ballvelocity (velocityof ball)
balldirection (getAngle ballvelocity)
ballprojline (linedirection ballpoint balldirection)
ballprojverticalpt (linelineintersection ballprojline vertgoalline)
ballprojinterseptpt (pointdirectiondistance ballprojverticalpt ballpoint (* 0.3 (pointpointdistance ballprojverticalpt ballpoint)))

#attacker geometry
attacker (closest enemy ballpoint loose)
attackerpt (pointof attacker)
attackerorient (orientationof attacker)
attackerline (linedirection attackerpt attackerorient)
attackerverticalpt (linelineintersection attackerline vertgoalline)
attackerinterseptpt (pointdirectiondistance attackerverticalpt ballpoint (* 0.3 (pointpointdistance attackerverticalpt ballpoint)))

# should the robot try to kick the ball?
ballzone (circle ballpoint 0.5)
robotshouldkick (and (< (pointpointdistance defensebotpt ballpoint) 0.4) (= (numberOfRobotsInACircle ballzone friendly) 2))

# Places to kick the ball
toptarget (point (/ (const-double FIELD_WIDTH) 2) (/ (const-double FIELD_HEIGHT) 2))
bottarget (point (/ (const-double FIELD_WIDTH) 2) (/ (const-double FIELD_HEIGHT) -2))

kickaction (if_Action (pathClear ballpoint (theirgoal) 2.0) (robotpointkick defensebot (theirgoal)) (if_Action (pathClear ballpoint bottarget 2.0) (robotpointkick defensebot bottarget) (robotpointkick defensebot toptarget)))
attackeraction (robotpointpointmove defensebot attackerinterseptpt ballpoint)
projballaction (robotpointpointmovenoavoid defensebot ballprojinterseptpt ballpoint)
ballaction (robotpointpointmove defensebot ballverticalpt ballpoint)

attackeraimsgoal (and (> (getYcoord attackerverticalpt) (getYcoord goalbotpt)) (< (getYcoord attackerverticalpt) (getYcoord goaltoppt)))
attackerhasball (< (pointpointdistance ballpoint attackerpt) 0.12)
ballaimsgoal (and (> (getYcoord ballprojverticalpt) (getYcoord goalbotpt)) (< (getYcoord ballprojverticalpt) (getYcoord goaltoppt)))
ballmoves (> (pointpointdistance ballvelocity (point 0 0)) 0.05)

Conditions:
Actions:
(if_Action robotshouldkick kickaction (if_Action (and attackeraimsgoal attackerhasball) attackeraction (if_Action  (and ballaimsgoal ballmoves) projballaction ballaction)))