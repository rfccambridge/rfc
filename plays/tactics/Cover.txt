Metadata:
type tactic
name Cover

Parameters:

Objects:
robot0 (closest friendly (ourgoal) loose)
robotpt (pointof robot0)
goaltoppt (point GOALIE_X_POS 0.35)
goalbotpt (point GOALIE_X_POS -0.35)

#goal geometry
GOALIE_X_POS (- (const-double GOALIE_X_POS_DELTA) (/ (const-double FIELD_WIDTH) 2))
endtopgoalpt (point GOALIE_X_POS 1.6)
endbottomgoalpt (point GOALIE_X_POS -1.6)
vertgoalline (line endtopgoalpt endbottomgoalpt)
goalpt (point GOALIE_X_POS 0)
radius (+ 0.5 0.1)
radiusaroundgoal (circle goalpt radius)

#ball geometry
ballpoint (pointof ball)
ballgoalline (line ballpoint (ourgoal))
ballverticalpt (linelineintersection ballgoalline vertgoalline)
ballradiusintersectpt (linecircleintersection ballgoalline radiusaroundgoal -1)

#ball-velocity projected geometry
ballvelocity (velocityof ball)
balldirection (getAngle ballvelocity)
ballprojline (linedirection ballpoint balldirection)
ballprojverticalpt (linelineintersection ballprojline vertgoalline)
ballprojradiusintersectpt (linecircleintersection ballprojline radiusaroundgoal -1)

#attacker geometry
attacker (closest enemy ballpoint loose)
attackerpt (pointof attacker)
attackerorient (orientationof attacker)
attackerline (linedirection attackerpt attackerorient)
attackerverticalpt (linelineintersection attackerline vertgoalline)
attackerradiusintersectpt (linecircleintersection attackerline radiusaroundgoal -1)

# should the goalie try to kick the ball?
goalieshouldkick (pointInGoal ballpoint)

kickaction (robotpointkick robot0 (theirgoal))
attackeraction (robotpointpointmove robot0 attackerradiusintersectpt attackerpt)
projballaction (robotpointpointmovenoavoid robot0 ballprojradiusintersectpt ballpoint)
ballaction (robotpointpointmove robot0 ballradiusintersectpt ballpoint)

attackeraimsgoal (and (> (getYcoord attackerverticalpt) (getYcoord goalbotpt)) (< (getYcoord attackerverticalpt) (getYcoord goaltoppt)))
attackerhasball (< (pointpointdistance ballpoint attackerpt) 0.12)
ballaimsgoal (and (> (getYcoord ballprojverticalpt) (getYcoord goalbotpt)) (< (getYcoord ballprojverticalpt) (getYcoord goaltoppt)))
ballmoves (> (pointpointdistance ballvelocity (point 0 0)) 0.05)

Conditions:
Actions:
(if_Action goalieshouldkick kickaction (if_Action (and attackeraimsgoal attackerhasball) attackeraction (if_Action  (and ballaimsgoal ballmoves) projballaction ballaction)))