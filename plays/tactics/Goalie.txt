Metadata:
type tactic
name Goalie

Parameters:
bot robot
shooter robot

Objects:
goaltoppt (point GOALIE_X_POS 0.35)
goalbotpt (point GOALIE_X_POS -0.35)

#goal geometry
GOALIE_X_POS (- (const-double GOALIE_X_POS_DELTA) (/ (const-double FIELD_WIDTH) 2))
endtopgoalpt (point GOALIE_X_POS 1.6)
endbottomgoalpt (point GOALIE_X_POS -1.6)
vertgoalline (line endtopgoalpt endbottomgoalpt)

#ball geometry
ballpoint (pointof ball)
ballgoalline (line ballpoint (ourgoal))
ballverticalpt (linelineintersection ballgoalline vertgoalline)

#ball-velocity projected geometry
ballvelocity (velocityof ball)
balldirection (getAngle ballvelocity)
ballprojline (linedirection ballpoint balldirection)
ballprojverticalpt (linelineintersection ballprojline vertgoalline)

#shooter geometry
shooterpt (pointof shooter)
shooterorient (orientationof shooter)
shooterline (linedirection shooterpt shooterorient)
shooterverticalpt (linelineintersection shooterline vertgoalline)

# should the goalie try to kick the ball?
goalieshouldkick (pointInGoal ballpoint)

kickaction (robotpointkick bot (theirgoal))
shooteraction (robotpointpointmovenoavoid bot shooterverticalpt shooterpt)
projballaction (robotpointpointmovenoavoid bot ballprojverticalpt ballpoint)
ballaction (robotpointpointmovenoavoid bot ballverticalpt ballpoint)
topaction (robotpointpointmovenoavoid bot goaltoppt ballpoint)
bottomaction (robotpointpointmovenoavoid bot goalbotpt ballpoint)

shooteraimsgoal (and (> (getYcoord shooterverticalpt) (getYcoord goalbotpt)) (< (getYcoord shooterverticalpt) (getYcoord goaltoppt)))
shooterhasball (< (pointpointdistance ballpoint shooterpt) 0.12)
ballaimsgoal (and (> (getYcoord ballprojverticalpt) (getYcoord goalbotpt)) (< (getYcoord ballprojverticalpt) (getYcoord goaltoppt)))
ballmoves (> (pointpointdistance ballvelocity (point 0 0)) 0.05)

Conditions:
Actions:
(if_Action goalieshouldkick kickaction (if_Action (and shooteraimsgoal shooterhasball) shooteraction (if_Action  (and ballaimsgoal ballmoves) projballaction (if_Action (< (getYcoord ballverticalpt) (getYcoord goalbotpt)) bottomaction (if_Action (> (getYcoord ballverticalpt) (getYcoord goaltoppt)) topaction ballaction)))))